plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.plugin.compose'
}

import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

def loadProperties() {
    def props = new Properties()
    file('../.env').withInputStream {
        props.load(it)
    }
    return props
}

def properties = loadProperties()

// Storefront API
def storefrontDomain = properties.getProperty("STOREFRONT_DOMAIN")
def accessToken = properties.getProperty("STOREFRONT_ACCESS_TOKEN")

// Customer Account API
def customerAccountApiClientId = properties.getProperty("CUSTOMER_ACCOUNT_API_CLIENT_ID")
def customerAccountApiRedirectUri = properties.getProperty("CUSTOMER_ACCOUNT_API_REDIRECT_URI")

def customerAccountApiGraphQLBaseUrl = properties.getProperty("CUSTOMER_ACCOUNT_API_GRAPHQL_BASE_URL")
def customerAccountApiAuthBaseUrl = properties.getProperty("CUSTOMER_ACCOUNT_API_AUTH_BASE_URL")

// Checkout Authentication (optional)
def checkoutAuthEndpoint = properties.getProperty("CHECKOUT_AUTH_ENDPOINT", "")
def checkoutAuthClientId = properties.getProperty("CHECKOUT_AUTH_CLIENT_ID", "")
def checkoutAuthClientSecret = properties.getProperty("CHECKOUT_AUTH_CLIENT_SECRET", "")

if (!storefrontDomain || !accessToken) {
    println("**** Please add a .env file with STOREFRONT_DOMAIN and STOREFRONT_ACCESS_TOKEN set *****")
}

android {
    namespace = 'com.shopify.checkout_sdk_mobile_buy_integration_sample'
    compileSdk = 36

    defaultConfig {
        applicationId "com.shopify.checkout_sdk_mobile_buy_integration_sample"
        minSdk = 28
        targetSdk = 36
        versionCode = 34
        versionName = "0.0.${versionCode}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }
        buildConfigField "String", "storefrontDomain", "\"$storefrontDomain\""
        buildConfigField "String", "storefrontAccessToken", "\"$accessToken\""
        buildConfigField "String", "customerAccountApiClientId", "\"$customerAccountApiClientId\""
        buildConfigField "String", "customerAccountApiRedirectUri", "\"$customerAccountApiRedirectUri\""
        buildConfigField "String", "customerAccountApiAuthBaseUrl", "\"$customerAccountApiAuthBaseUrl\""
        buildConfigField "String", "customerAccountApiGraphQLBaseUrl", "\"$customerAccountApiGraphQLBaseUrl\""
        buildConfigField "String", "checkoutAuthEndpoint", "\"$checkoutAuthEndpoint\""
        buildConfigField "String", "checkoutAuthClientId", "\"$checkoutAuthClientId\""
        buildConfigField "String", "checkoutAuthClientSecret", "\"$checkoutAuthClientSecret\""
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable = true
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    buildFeatures {
        compose = true
        buildConfig = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.8'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

tasks.withType(KotlinJvmCompile).configureEach {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_11
    }
}

dependencies {
    // Checkout SDK
    implementation project(':lib')

    implementation "androidx.appcompat:appcompat:$app_compat_version"
    implementation "androidx.core:core-ktx:$core_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.activity:activity-compose:$activity_version"
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.compose.material:material:$compose_ui_version"
    implementation "androidx.navigation:navigation-compose:$nav_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.datastore:datastore-preferences:$datastore_version"
    implementation "io.insert-koin:koin-androidx-compose:$koin_android_compose_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.compose.material3:material3-android:$material3_version"
    implementation "androidx.paging:paging-compose-android:$paging_version"

    annotationProcessor "androidx.room:room-compiler:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    // SDK facilitating calling the Shopify Storefront API
    implementation "com.shopify.mobilebuysdk:buy3:$mobile_buy_version"

    // Image loading library
    implementation "io.coil-kt:coil-compose:$coil_version"

    // Logging
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlin_serialization_version"

    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
}
