import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

plugins {
    id 'signing'
    id 'maven-publish'
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'io.gitlab.arturbosch.detekt'
}

def resolveEnvVarValue(name, defaultValue) {
    def rawValue = providers.environmentVariable(name).getOrElse("")
    return rawValue ? rawValue : defaultValue
}

def versionName = resolveEnvVarValue("CHECKOUT_SHEET_KIT_VERSION", "4.0.0-SNAPSHOT")

ext {
    app_compat_version = '1.7.1'
    kotlin_serialization_version = '1.6.3'

    androidx_test_version = '1.7.0'
    androidx_junit_ext_version = '1.3.0'
    androidx_webkit_version = '1.14.0'

    junit_version = '4.13.2'
    robolectric_version = '4.16'
    mockito_core_version = '4.11.0'
    mockito_kotlin_version = '4.1.0'
    assertj_version = '3.27.6'
    awaitility_version = '4.3.0'
}

android {
    namespace = 'com.shopify.checkoutsheetkit'
    compileSdk = 36

    defaultConfig {
        minSdk = 23
        targetSdk = 35
        versionCode = 1
        versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "SDK_VERSION", "\"$versionName\""
    }

    lintOptions {
        checkDependencies = true
        warningsAsErrors = true
        warning 'LintBaseline'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
    buildFeatures {
        buildConfig = true
    }
}

tasks.withType(KotlinJvmCompile).configureEach {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_1_8)
        if (!name.contains("Test")) {
            freeCompilerArgs.add("-Xexplicit-api=strict")
        }
    }
}

dependencies {
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    // noinspection NewerVersionAvailable -- mockito 5 requires jvm target 1.11
    testImplementation "org.mockito:mockito-core:$mockito_core_version"
    // noinspection NewerVersionAvailable -- mockito 5 requires jvm target 1.11
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockito_kotlin_version"
    // noinspection NewerVersionAvailable -- mockito 5 requires jvm target 1.11
    testImplementation "org.mockito:mockito-android:$mockito_core_version"
    testImplementation "org.assertj:assertj-core:$assertj_version"
    testImplementation "org.awaitility:awaitility:$awaitility_version"

    androidTestImplementation "androidx.test:core-ktx:$androidx_test_version"
    androidTestImplementation "androidx.test.ext:junit-ktx:$androidx_junit_ext_version"

    //Implementation
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlin_serialization_version"
    implementation "androidx.appcompat:appcompat:$app_compat_version"
    implementation "androidx.webkit:webkit:$androidx_webkit_version"
}

signing {
    def signingKeyId = findProperty("signingKeyId")
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")

    // Only sign publications when credentials are present
    if (signingKeyId && signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign publishing.publications
    }
}

detekt {
    buildUponDefaultConfig = true
    config.setFrom('detekt.config.yml')
}


project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                pom {
                    name = "CheckoutSheetKit"
                    description = "Shopify's Checkout Sheet Kit makes it simple to render checkouts inside your mobile app."
                    url = "https://github.com/Shopify/checkout-sheet-kit-android"
                    groupId = "com.shopify"
                    artifactId = "checkout-sheet-kit"
                    version = versionName

                    licenses {
                        license {
                            name = "MIT"
                            url = "https://opensource.org/licenses/MIT"
                        }
                    }

                    developers {
                        developer {
                            name = "Shopify Inc."
                        }
                    }

                    scm {
                        connection = "https://github.com/Shopify/checkout-sheet-kit-android.git"
                        developerConnection = "https://github.com/Shopify/checkout-sheet-kit-android.git"
                        url = "https://github.com/Shopify/checkout-sheet-kit-android.git"
                    }
                }

                afterEvaluate {
                    from components.release
                }
            }
        }

        repositories {
            maven {
                name = 'ossrh-staging-api'
                url = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
                credentials {
                    username = System.getenv("OSSRH_USERNAME")
                    password = System.getenv("OSSRH_PASSWORD")
                }
            }
        }
    }
}
