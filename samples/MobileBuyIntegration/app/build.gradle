plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

def loadProperties() {
    def props = new Properties()
    file('../.env').withInputStream {
        props.load(it)
    }
    return props
}

def properties = loadProperties()
def storefrontDomain = properties.getProperty("STOREFRONT_DOMAIN")
def accessToken = properties.getProperty("STOREFRONT_ACCESS_TOKEN")

if (!storefrontDomain || !accessToken) {
    println("**** Please add a .env file with STOREFRONT_DOMAIN and STOREFRONT_ACCESS_TOKEN set *****")
}

android {
    namespace 'com.shopify.checkout_sdk_mobile_buy_integration_sample'
    compileSdk 34

    defaultConfig {
        applicationId "com.shopify.checkout_sdk_mobile_buy_integration_sample"
        minSdk 23
        targetSdk 34
        versionCode 21
        versionName "0.0.${versionCode}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField('String', 'storefrontDomain', '"' + storefrontDomain + '"')
            buildConfigField('String', 'storefrontAccessToken', '"' + accessToken + '"')
        }
        debug {
            debuggable true
            buildConfigField('String', 'storefrontDomain', '"' + storefrontDomain + '"')
            buildConfigField('String', 'storefrontAccessToken', '"' + accessToken + '"')
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.8'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // Checkout SDK
    implementation project(':lib')

    implementation "androidx.appcompat:appcompat:$app_compat_version"
    implementation "androidx.core:core-ktx:$core_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.activity:activity-compose:$activity_version"
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.compose.material:material:$compose_ui_version"
    implementation "androidx.navigation:navigation-compose:$nav_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.datastore:datastore-preferences:$datastore_version"
    implementation "io.insert-koin:koin-androidx-compose:$koin_android_compose_version"

    // SDK facilitating calling the Shopify Storefront API
    implementation("com.shopify.mobilebuysdk:buy3:$mobile_buy_version") {
        exclude group: 'com.shopify.graphql.support', module: 'support'
    }

    // Image loading library
    implementation "io.coil-kt:coil-compose:$coil_version"

    // Serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlin_serialization_version"

    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
}
